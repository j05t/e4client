apply plugin: 'com.android.application'

def apikeyPropertiesFile = rootProject.file("apikeys.properties")
def apikeyProperties = new Properties()
apikeyProperties.load(new FileInputStream(apikeyPropertiesFile))

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.2"
    defaultConfig {
        applicationId "com.jstappdev.e4client"
        minSdkVersion 24
        targetSdkVersion 29
        versionCode 1
        versionName "0.1"

        buildConfigField("String", "EMPATICA_API_KEY", apikeyProperties['EMPATICA_API_KEY'])
        buildConfigField("String", "SCICHART_LICENSE", apikeyProperties['SCICHART_LICENSE'])
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    splits {

        // Configures multiple APKs based on ABI.
        abi {

            // Enables building multiple APKs per ABI.
            enable true

            // Resets the list of ABIs that Gradle should create APKs for to none.
            reset()

            // Specifies a list of ABIs that Gradle should create APKs for.
            include 'armeabi-v7a', 'x86', 'x86_64'

            // Specifies that we do not want to also generate a universal APK that includes all ABIs.
            universalApk false
        }
    }
}

repositories {
    flatDir {
        dirs 'libs'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.navigation:navigation-fragment:2.1.0'
    implementation 'androidx.navigation:navigation-ui:2.1.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.1.0'

    // empatica
    implementation 'com.empatica.empalink:empalink:2.2@aar'
    implementation 'com.squareup.okhttp:okhttp:2.7.5'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'

    // scichart
    implementation (name:'charting-release', ext:'aar')
    implementation (name:'drawing-release', ext:'aar')
    implementation (name:'data-release', ext:'aar')
    implementation (name:'core-release', ext:'aar')
    implementation (name:'extensions-release', ext:'aar')

    //noinspection AnnotationProcessorOnCompilePath
    implementation 'com.jakewharton:butterknife:10.2.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:10.2.0'

    // http client
    implementation "com.squareup.okhttp3:okhttp-urlconnection:4.2.0"

    // ZIP library
    implementation 'net.lingala.zip4j:zip4j:2.1.4'

    // Google Fit
    implementation 'com.google.android.gms:play-services-fitness:18.0.0'
    implementation 'com.google.android.gms:play-services-auth:17.0.0'
}
